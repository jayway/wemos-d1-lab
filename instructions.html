<html>

<h1>
    Wemos D1 mini
</h1>

<h2>
     a mini wifi board based on ESP-8266EX
</h2>

<img src="d1miniv2.jpg" height="200" />

<h3>
    Introduction
</h3>

<p>
    We'll be making wifi-enabled sensors using the Wemos D1 mini V2. There are 2 types of shields available:
</p>

<ul>
    <li>A <a href="https://www.wemos.cc/product/dht-shield.html">temperature</a> shield</li>
    <li>A <a href="https://www.wemos.cc/product/protoboard.html">prototyping</a> shield. Can be used to connect your own sensors</li>
</ul>

<p>
    The goal is to have each sensor
    <ol>
        <li>connected to the network broadcasting sensor data</li>
        <li>without hard-coded credentials</li>
        <li>packaged into a standalone unit together with power adapter (5V)</li>
    </ol>
</p>

<h3>
    Preperations
</h3>

<p>You should already have PlatformIO installed, but if you don't, see the <a href="https://github.com/jayway/platformio-lab">PlatformIO lab</a></p>
<ul>
    <li>
        Install <a href="https://www.wemos.cc/product/d1-mini.html">Wemos D1 mini</a> drivers if needed (only Mac & Windows). Note that older versions of the driver will cause a kernel panic on Mac. Drivers can be found <a href="https://www.wemos.cc/downloads">here</a> and <a href="https://blog.sengotta.net/signed-mac-os-driver-for-winchiphead-ch340-serial-bridge/">here</a>.
    </li>
</ul>

<h3>
    Lab 1 - getting started
</h3>

<p>
    Create a new project in PlatformIO for your Wemos D1 mini, add code to make the standard board LED blink, then upload & run.
</p>

<pre>
<code>
#include &lt;Arduino.h&gt;

int blink = LOW;

void setup()
{
  Serial.begin(115200);
  pinMode(LED_BUILTIN, OUTPUT);
  digitalWrite(LED_BUILTIN, blink);
  Serial.println();
}

void loop()
{
  Serial.print(".");
  blink = (blink == LOW ? HIGH : LOW);
  digitalWrite(LED_BUILTIN, blink);
  delay(1000);
}
</code>
</pre>

<h3>
    Lab 2 - connecting to Wifi
</h3>

<p>
    Add code to connect to the local WIFI. The very neat <a href="https://github.com/tzapu/WiFiManager">WiFiManager</a> automatically acts as an AP if unable to connect to last known network.
</p>

<pre>
<code>
#include &lt;Arduino.h&gt;
#include &lt;WiFiManager.h&gt;

char name[] = "mywemos";

void configModeCallback (WiFiManager *myWiFiManager) {
  Serial.println("Entered AP mode for configuration");
  Serial.print("SSID: ");
  Serial.println(myWiFiManager->getConfigPortalSSID());
  Serial.print("IP address: ");
  Serial.println(WiFi.softAPIP());
}

void setup()
{
  Serial.begin(115200);

  WiFiManager wifiManager;

  // for debugging
  //wifiManager.resetSettings();

  wifi_station_set_hostname(name);

  // set callback that gets called when connecting to previous WiFi fails,
  // and enters AP mode
  wifiManager.setAPCallback(configModeCallback);

  // fetches ssid and pass from eeprom and tries to connect
  // if it does not connect it starts an access point with the specified name
  // and goes into a blocking loop awaiting configuration
  if(!wifiManager.autoConnect()) {
    Serial.println("failed to connect and hit timeout");
    // reset and try again
    ESP.reset();
    delay(1000);
  }

  Serial.println("Connected!");
  Serial.print("SSID: ");
  Serial.println(WiFi.SSID());
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());
  Serial.print("MAC: ");
  Serial.println(WiFi.macAddress());
  Serial.print("Hostname: ");
  Serial.println(WiFi.hostname());
  Serial.print("GW IP: ");
  Serial.println(WiFi.gatewayIP());
}

void loop()
{
  Serial.print(".");
  delay(5000);
}
</code>
</pre>

<h3>
    Lab 3 - a simple web server
</h3>

<p>
    Letting the D1 mini act as a webserver can be handy for configuration purposes, e.g to passing over security credentials. There is a simple web server called <a href="https://github.com/esp8266/Arduino/tree/master/libraries/ESP8266WebServer">ESP8266WebServer</a> in the Arduino libraries.
</p>

<pre>
<code>
#include &lt;Arduino.h&gt;
#include &lt;WiFiManager.h&gt;
#include &lt;ESP8266WebServer.h&gt;

char name[] = "mywemos";
ESP8266WebServer server(80);

void handleRoot()
{
  server.send(200, "text/plain", "hello from esp8266!");
}

void setup()
{
  Serial.begin(115200);
  WiFiManager wifiManager;
  wifi_station_set_hostname(name);
  wifiManager.autoConnect();

  // register handlers (can both be function pointers or lambdas)
  server.on("/", handleRoot);
  server.onNotFound([](){
      server.send(404, "text/plain", "404 Not found");
  });

  // start server
  server.begin();
  Serial.println("HTTP server started");
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());
}

void loop()
{
  // handle next request
  server.handleClient();
}
</code>
</pre>

<h3>
    Lab 4 - reading sensor data
</h3>

<p>
    No point in having a sensor without reading sensor data. This time we need to import an external library; the <a href="https://github.com/adafruit/Adafruit_DHT_Unified">Adafruit DHT Unified</a>. PlatformIO has made it available as a library dependency, which you add in the <a href="http://docs.platformio.org/en/stable/projectconf.html">platformio.ini</a> file.
</p>

<pre>
<code>
#include &lt;Arduino.h&gt;
#include &lt;DHT.h&gt;

#define DHTPIN 2 /* This (GPIO 2) is pin D4 */
#define DHTTYPE DHT11

DHT dht(DHTPIN, DHTTYPE);
unsigned long last_sensor_read = 0;

void readData()
{
  pinMode(DHTPIN, INPUT_PULLUP);
  float humidity = dht.readHumidity();
  float temperature = dht.readTemperature();

  Serial.print("Humidity: ");
  Serial.print(humidity);
  Serial.print(" %\t");
  Serial.print("Temperature: ");
  Serial.print(temperature);
  Serial.println(" (C)");
}

void setup()
{
  Serial.begin(115200);
  readData();
  last_sensor_read = millis();
}

void loop()
{
  unsigned long current_time = millis();
  long time_until_next_read = (last_sensor_read + 5000) - current_time;
  if (time_until_next_read > 0)
  {
    Serial.print("Sleeping for ");
    Serial.print(time_until_next_read);
    Serial.println(" millis");
    delay(time_until_next_read);
    return;
  }
  readData();
  last_sensor_read = current_time;
}
</code>
</pre>

<h3>
    Lab 5 - MQTT
</h3>

<p>
    A proper IoT network sensor must be able to talk MQTT. This lab will use MQTT over HTTP without encryption.
</p>

<pre>
<code>
#include &lt;Arduino.h&gt;
#include &lt;WiFiManager.h&gt;
#include &lt;PubSubClient.h&gt;

WiFiClient wifiClient;
PubSubClient mqtt_client(wifiClient);

String clientId = "ESP-" + String(ESP.getChipId());
bool led_state;
unsigned long last_time;
const char* out_topic = "esp8266/hello";
const char* led_topic = "esp8266/led";

void updateLed()
{
  digitalWrite(LED_BUILTIN, led_state ? LOW : HIGH);
}

void mqttCallback(char* topic, unsigned char* payload, unsigned int length)
{
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  for (int i = 0; i < length; i++)
  {
    Serial.print((char)payload[i]);
  }
  Serial.println();

  // Switch on the LED if an 1 was received as first character
  led_state = ( (char)payload[0] == '1' );
  updateLed();
}

void mqtt_reconnect()
{
  while (!mqtt_client.connected())
  {
    Serial.print("Attempting MQTT connection...");

    // Attempt to connect
    if (mqtt_client.connect(clientId.c_str())) {
      Serial.println("connected");

      mqtt_client.publish(out_topic, "hello world");
      mqtt_client.subscribe(led_topic);

    }
    else
    {
      Serial.print("failed, rc=");
      Serial.print(mqtt_client.state());
      Serial.println(" try again in 5 seconds");
      delay(5000);
    }
  }
}

void setup()
{
  Serial.begin(115200);

  pinMode(LED_BUILTIN, OUTPUT);
  led_state = false;
  updateLed();

  WiFiManager wifiManager;
  wifi_station_set_hostname((char*)clientId.c_str());
  wifiManager.autoConnect();

  mqtt_client.setServer("com.example.mqtt", 1883);
  mqtt_client.setCallback(mqttCallback);
}

void loop()
{
  if (!mqtt_client.connected())
  {
    mqtt_reconnect();
  }
  mqtt_client.loop();

  long current_time = millis();
  if (current_time - last_time > 5000)
  {
    last_time = current_time;
    String msg = "hello " + String(last_time);
    Serial.print("Publish message: ");
    Serial.println(msg);
    mqtt_client.publish(out_topic, msg.c_str());
  }
}
</code>
</pre>

<h3>
    Lab 6 - Tying it all together
</h3>

<p>
    Using the functionality from previous labs, create a networked sensor that broadcasts temperature and humidity to the local MQTT broker.
</p>

<h3>
    Lab 7 - Using AWS IoT
</h3>

<p>
  The <a href="https://aws.amazon.com/iot/sdk/">AWS IoT device SDKs</a> currently does not support the ESP8266. One problem is that AWS IoT devices are authenticated using X.509 certificates. See <a href="http://docs.aws.amazon.com/iot/latest/developerguide/iot-security-identity.html">documentation</a>. In short, the device must support:
  <ul>
    <li>TLS1.2</li>
    <li>SHA-256 RSA certificate signature validation</li>
    <li>One of the cipher suites from the TLS cipher suite support section</li>
  </ul>
  Neither the SDK from <a href="https://espressif.com/en/support/download/sdks-demos">Espressif</a> nor the <a href="https://github.com/esp8266/Arduino">Arduino Core</a> implementation currently support TLS1.2.   
</p>

<p>
  There are different approaches to supporting AWS IoT service. Listing some below:
  <ul>
    <li>Arduino Core dev branch has a version of axTLS that support TLS1.2. The <a href="https://github.com/aws/aws-iot-device-sdk-embedded-C">AWS IoT Device SDK for embedded C</a> uses mbedTLS. Maybe it is possible to use axTLS instead?</li>
    <li>AWS also have a new <a href="https://github.com/aws/aws-iot-device-sdk-cpp">Device SDK for C++</a> that has an abstract Network layer allowing different TLS libraries. Currently, there are reference implementations for openSSL, mbedTLS and Websockets. Maybe easier to port this?</li>
    <li>One guy have cheated a little by using AWS IAM user credentials together with websockets to connect to AWS IoT. Requires you to hardcode the credentials in the code. Check it out <a href="https://github.com/odelot/aws-mqtt-websockets">here</a></li>
    <li>Other initiatives that uses AWS IAM user credentials to connect to AWS services <a href="https://github.com/heskew/aws-sdk-arduino">here</a></li>
    <li>A kind of wild idea is to use a completely different RTOS on the device, see <a href="http://blogs.paasmer.co/series-blogs-connect-esp-8266-nodemcu-aws-iot/">ESP-OPEN-RTOS</a>
  </ul>
</p>

</html>